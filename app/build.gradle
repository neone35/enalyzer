apply plugin: 'com.android.application' // main android plugin

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.neone35.enalyzer"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                /* room schema export */
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support_lib_v = '28.0.0-rc01'
    aac_v = '1.1.1'
    retrofit_v = '2.4.0'
    glide_v = '4.7.1'
    butterknife_v = '8.8.1'
    stetho_v = '1.5.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-v4:$support_lib_v"

    // dependent on v4
    implementation "com.android.support:appcompat-v7:$support_lib_v"

    // material design components
    implementation "com.android.support:design:$support_lib_v"

    // VectorDrawableCompat
    implementation "com.android.support:support-vector-drawable:$support_lib_v"
    implementation "com.android.support:recyclerview-v7:$support_lib_v"
    implementation "com.android.support:cardview-v7:$support_lib_v"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:multidex:1.0.3'

    // GOOGLE PLAY SERVICES
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-ml-vision:17.0.0'

    // ANDROID ARCHITECTURE COMPONENTS

    // ViewModel & LiveData
    implementation "android.arch.lifecycle:runtime:$aac_v"
    implementation "android.arch.lifecycle:extensions:$aac_v"

    // Compat with support library
    annotationProcessor "android.arch.lifecycle:compiler:$aac_v"

    // database
    annotationProcessor "android.arch.persistence.room:compiler:$aac_v"
    implementation "android.arch.persistence.room:runtime:$aac_v"

    // 3RD PARTY LIBRARIES

    // parsing
    implementation 'com.google.code.gson:gson:2.8.5'

    // network calls
    implementation "com.squareup.retrofit2:retrofit:$retrofit_v"

    // gson & retrofit
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_v"

    // logging
    implementation 'com.orhanobut:logger:2.2.0'

    // loading images
    implementation "com.github.bumptech.glide:glide:$glide_v"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_v"

    // helper libraries from Google
    api 'com.google.guava:guava:25.1-android'

    // helper functions
    implementation 'com.blankj:utilcode:1.17.3'

    // R class binding
    implementation "com.jakewharton:butterknife:$butterknife_v"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_v"

    //network debugging in chrome
    implementation "com.facebook.stetho:stetho:$stetho_v"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_v"

    // UI LIBRARIES

    // UI view for scanActivity
    implementation 'com.beloo.widget:ChipsLayoutManager:0.3.7@aar'

    // TextView for expandable long text
    implementation 'at.blogc:expandabletextview:1.0.5'

    // TEST DEPS
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //OTHER
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
}

// must be at the bottom to avoid dependency clash
apply plugin: 'com.google.gms.google-services' // google play services

