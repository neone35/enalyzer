apply plugin: 'com.android.application' // main android plugin

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.github.neone35.enalyzer"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                /* room schema export */
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

ext {
    aac_v = '2.1.0-alpha01'
    retrofit_v = '2.4.0'
    glide_v = '4.8.0'
    butterknife_v = '10.0.0'
    stetho_v = '1.5.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // dependent on v4
    implementation "androidx.appcompat:appcompat:1.1.0-alpha01"

    // material design components
    implementation "com.google.android.material:material:1.1.0-alpha02"

    // VectorDrawableCompat
    implementation "androidx.vectordrawable:vectordrawable:1.1.0-alpha01"
    implementation "androidx.recyclerview:recyclerview:1.1.0-alpha01"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'

    // GOOGLE PLAY SERVICES
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-ml-vision:18.0.2'

    // ANDROID ARCHITECTURE COMPONENTS

    // ViewModel & LiveData
    implementation "androidx.lifecycle:lifecycle-runtime:$aac_v"
    implementation "androidx.lifecycle:lifecycle-extensions:$aac_v"

    // Compat with support library
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$aac_v"

    // database
    annotationProcessor "androidx.room:room-compiler:$aac_v"
    implementation "androidx.room:room-runtime:$aac_v"

    // 3RD PARTY LIBRARIES

    // parsing
    implementation 'com.google.code.gson:gson:2.8.5'

    // network calls
    implementation "com.squareup.retrofit2:retrofit:$retrofit_v"

    // gson & retrofit
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_v"

    // logging
    implementation 'com.orhanobut:logger:2.2.0'

    // loading images
    implementation "com.github.bumptech.glide:glide:$glide_v"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_v"

    // helper libraries from Google
    api 'com.google.guava:guava:25.1-android'

    // helper functions
    implementation 'com.blankj:utilcode:1.17.3'

    // R class binding
    implementation "com.jakewharton:butterknife:$butterknife_v"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_v"

    //network debugging in chrome
    implementation "com.facebook.stetho:stetho:$stetho_v"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_v"

    // UI LIBRARIES

    // UI view for scanActivity
    implementation 'com.beloo.widget:ChipsLayoutManager:0.3.7@aar'

    // TextView for expandable long text
    implementation 'at.blogc:expandabletextview:1.0.5'

    // TEST DEPS
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    //OTHER
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
}

// must be at the bottom to avoid dependency clash
apply plugin: 'com.google.gms.google-services' // google play services

